SPICE JFET level 1 ring oscillator (verilog-A)

load "spice/jfet1.osdi"
load "resistor.osdi"
load "capacitor.osdi"

model v vsource
model i isource
model r resistor
model c capacitor

model nf sp_jfet1 type=1 vt0=-4 beta=2m lambda=0.05 rs=0.1 rd=0.1 cgs=10p cgd=20p kf=1e-14 af=1

subckt inv(in out vdd vss)
  rd (vdd out) r r=1k
  j1 (out in s) nf
  rs (s vss) r r=1k
  rg (in vss) r r=10k
  cblk (s vss) c c=100n
ends

c1 (a1 b1) c c=10n
x1 (a1 b2 vdd 0) inv
c2 (a2 b2) c c=10n
x2 (a2 b3 vdd 0) inv
c3 (a3 b3) c c=10n
x3 (a3 b4 vdd 0) inv
c4 (a4 b4) c c=10n
x4 (a4 b5 vdd 0) inv
c5 (a5 b5) c c=10n
x5 (a5 b1 vdd 0) inv

vdd (vdd 0) v dc=10

control
  options nr_contbypass=0 nr_residualcheck=0
  analysis tran1 tran step=1u stop=1m maxstep=1n icmode="uic"

  print stats

  postprocess(PYTHON, "runme.py")
endc

embed "runme.py" <<<FILE
from rawfile import rawread
import numpy as np
import matplotlib.pyplot as plt
import sys

raw = rawread('tran1.raw')
plot = raw.get()

fig1 = plt.figure(figsize=(8,4), dpi=100)
fig1.suptitle('N-JFET ring oscillator')
ax_dict = fig1.subplot_mosaic("""
A
""")

ax_dict["A"].set_title('Output')
ax_dict["A"].set_ylabel('Vin [V]')
ax_dict["A"].set_xlabel('time [s]')
ax_dict["A"].plot(plot['time'], plot['b1'])
ax_dict["A"].plot(plot['time'], plot['b2'])
ax_dict["A"].plot(plot['time'], plot['b3'])

fig1.tight_layout()


plt.show()
>>>FILE


