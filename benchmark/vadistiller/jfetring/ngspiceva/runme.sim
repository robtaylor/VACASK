SPICE JFET level 1 ring oscillator (Verilog-A)

.model nf sp_jfet1 type=1 vt0=-4 beta=2m lambda=0.05 rs=0.1 rd=0.1 cgs=10p cgd=20p kf=1e-14 af=1
.model r sp_resistor
.model c sp_capacitor

.subckt inv in out vdd vss
  nrd vdd out r r=1k
  nj1 out in s nf
  nrs s vss r r=1k
  nrg in vss r r=10k
  ncblk s vss c c=100n
.ends

nc1 a1 b1 c c=10n
x1 a1 b2 vdd 0 inv
nc2 a2 b2 c c=10n
x2 a2 b3 vdd 0 inv
nc3 a3 b3 c c=10n
x3 a3 b4 vdd 0 inv
nc4 a4 b4 c c=10n
x4 a4 b5 vdd 0 inv
nc5 a5 b5 c c=10n
x5 a5 b1 vdd 0 inv

vdd vdd 0 10

.options klu
.control
  pre_osdi jfet1.osdi
  pre_osdi resistor.osdi
  pre_osdi capacitor.osdi
  tran 1u 1m 0 1n uic
  * plot b1 b2 b3
  rusage all
  set noaskquit
  quit
.endc

.end
